package net.project.parser.scenarios;

import java_cup.runtime.*;
import java.util.ArrayList;
import java.util.HashMap;
import net.project.utils.Scenario;
import net.project.utils.GenericAttr;
import net.project.utils.GenericElement;

parser code{:
    public boolean hasUnRecoveredSyntaxError = false;
    public ArrayList<HashMap<String, String>> errors = new ArrayList<>();
    public ArrayList<Scenario> scenarios = new ArrayList<>();
:}

// single chars
terminal

    // tags
    INIT_SCENARIO,
    END_SCENARIO,
    INIT_CHARACTERS,
    END_CHARACTERS,
    INIT_HEROES,
    END_HEROES,
    INIT_VILLAINS,
    END_VILLAINS,
    INIT_WALLS,
    END_WALLS,
    INIT_FLOOR,
    END_FLOOR,
    INIT_EXTRAS,
    END_EXTRAS,
    INIT_WEAPONS,
    END_WEAPONS,
    INIT_BONUS,
    END_BONUS,
    INIT_FINISH,
    END_FINISH,

    // single chars
    O_PAREN,
    C_PAREN,
    COMMA,
    DOT,
    EQUAL,
    SEMICOLON,
    MORE_THAN,

    // single words
    NAME,
    BG,
    HEIGHT,
    WIDTH

    // ER
    ;

terminal String
    ID;

terminal Integer
    INT;

non terminal
    scenarios,


    walls,
    walls_elements,
    walls_element,
    floors,
    floors_elements,
    floors_element,
    extras,
    extras_elements,
    extras_element,
    extras_weapons,
    weapons_elements,
    weapons_element,
    extras_bonus,
    bonus_elements,
    bonus_element,
    finish,
    finish_elements,
    finish_element,

    int_range;

non terminal String
    name,
    bg;

non terminal Integer
    height,
    width;

non terminal GenericAttr
    scenario_element,
    scenario_attr,
    characters_element,
    villains_element;


non terminal GenericElement
    scenario_elements,
    scenario_attrs,
    characters,
    characters_elements,
    heroes,
    villains,
    villains_elements;

non terminal Scenario
    scenario;

start with scenarios;

scenarios::= scenario:s{:
                this.parser.scenarios.add(s);
            :}
            | scenario:s scenarios{:
                this.parser.scenarios.add(s);
            :};

scenario::= INIT_SCENARIO scenario_attrs:attrs MORE_THAN scenario_elements:elements END_SCENARIO{:
    RESULT = new Scenario();
    RESULT.setName((String)attrs.getAttr("name").getValue());
    RESULT.setHeight((Integer)attrs.getAttr("height").getValue());
    RESULT.setWidth((Integer)attrs.getAttr("width").getValue());
    RESULT.setBg((String)attrs.getAttr("bg").getValue());
    RESULT.setElements(elements);
:};

scenario_attrs::= scenario_attr:attr SEMICOLON{:
                    RESULT = new GenericElement();
                    RESULT.addAttr(attr);
                :}
                | scenario_attr:attr SEMICOLON scenario_attrs:attrs{:
                    RESULT = new GenericElement();
                    RESULT.addAttr(attr);
                    RESULT.addAll(attrs);
                :};

scenario_attr::= name:n{:
                    RESULT = new GenericAttr<String>();
                    RESULT.setType("name");
                    RESULT.setValue(n);
                :}
                | bg:bg{:
                    RESULT = new GenericAttr<String>();
                    RESULT.setType("bg");
                    RESULT.setValue(bg);
                :}
                | height:height{:
                    RESULT = new GenericAttr<Integer>();
                    RESULT.setType("height");
                    RESULT.setValue(height);
                :}
                | width:width{:
                   RESULT = new GenericAttr<Integer>();
                   RESULT.setType("width");
                   RESULT.setValue(width);
               :};

name::= NAME EQUAL ID:id{: RESULT = id; :};

bg::= BG EQUAL ID:id{: RESULT = id; :};

height::= HEIGHT EQUAL INT:i{: RESULT = i; :};

width::= WIDTH EQUAL INT:i{: RESULT = i; :};

scenario_elements::= scenario_element:element{:
                        RESULT = new GenericElement();
                        RESULT.addAttr(element);
                    :}
                    | scenario_element:element scenario_elements:elements{:
                        RESULT = new GenericElement();
                        RESULT.addAttr(element);
                        RESULT.addAll(elements);
                    :};

scenario_element::= characters:c{:
                        RESULT = new GenericAttr<GenericElement>();
                        RESULT.setType("characters");
                        RESULT.setValue(c);
                    :}
                    | walls:w{:
                        RESULT = new GenericAttr();
                        RESULT.setType("walls");
                        RESULT.setValue(w);
                    :}
                    | floors:f{:
                        RESULT = new GenericAttr();
                        RESULT.setType("floors");
                        RESULT.setValue(f);
                    :}
                    | extras:e{:
                        RESULT = new GenericAttr();
                        RESULT.setType("extras");
                        RESULT.setValue(e);
                    :}
                    | finish:f{:
                        RESULT = new GenericAttr();
                        RESULT.setType("finish");
                        RESULT.setValue(f);
                    :};

characters::= INIT_CHARACTERS characters_elements:elements END_CHARACTERS{:
    RESULT = elements;
:};

characters_elements::= characters_element:element{:
                            RESULT = new GenericElement();
                            RESULT.addAttr(element);
                        :}
                        | characters_element:element characters_elements:elements{:
                            RESULT = new GenericElement();
                            RESULT.addAttr(element);
                            RESULT.addAll(elements);
                        :};

characters_element::= heroes:h{:
                            RESULT = new GenericAttr();
                            RESULT.setType("hero");
                            RESULT.setValue(h);
                        :}
                        | villains:v{:
                            RESULT = new GenericAttr();
                            RESULT.setType("villains");
                            RESULT.setValue(v);
                        :};

heroes::= INIT_HEROES ID:name O_PAREN INT:x COMMA INT:y C_PAREN SEMICOLON END_HEROES{:
    RESULT = new GenericElement();
    GenericAttr<String> n = new GenericAttr<>();
    n.setType("name");
    n.setValue(name);
    RESULT.addAttr(n);

    GenericAttr<Integer> tmpY = new GenericAttr<>();
    tmpY.setType("y");
    tmpY.setValue(y);
    RESULT.addAttr(tmpY);

    GenericAttr<Integer> tmpX = new GenericAttr<>();
    tmpX.setType("x");
    tmpX.setValue(x);
    RESULT.addAttr(tmpX);
:};

villains::= INIT_VILLAINS villains_elements:elements END_VILLAINS{:
    RESULT = elements;
:};

villains_elements::= villains_element:element{:
                        RESULT = new GenericElement();
                        RESULT.addAttr(element);
                    :}
                    | villains_element:element villains_elements:elements{:
                        RESULT = new GenericElement();
                        RESULT.addAttr(element);
                        RESULT.addAll(elements);
                    :};

villains_element::= ID:name O_PAREN INT:x COMMA INT:y C_PAREN SEMICOLON{:
    RESULT = new GenericAttr<GenericElement>();
    RESULT.setType("villain"); // unnecessary, I'm boring

    GenericElement villain = new GenericElement();

    GenericAttr<String> n = new GenericAttr<>();
    n.setType("name");
    n.setValue(name);
    villain.addAttr(n);

    GenericAttr<Integer> tmpY = new GenericAttr<>();
    tmpY.setType("y");
    tmpY.setValue(y);
    villain.addAttr(tmpY);

    GenericAttr<Integer> tmpX = new GenericAttr<>();
    tmpX.setType("x");
    tmpX.setValue(x);
    villain.addAttr(tmpX);

    RESULT.setValue(villain);

:};

walls::= INIT_WALLS walls_elements END_WALLS;

walls_elements::= walls_element
                | walls_element walls_elements;

walls_element::= ID O_PAREN int_range COMMA int_range C_PAREN SEMICOLON;

floors::= INIT_FLOOR floors_elements END_FLOOR;

floors_elements::= floors_element
                    | floors_element floors_elements;

floors_element::= ID O_PAREN int_range COMMA int_range C_PAREN SEMICOLON;

extras::= INIT_EXTRAS extras_elements END_EXTRAS;

extras_elements::= extras_element
                    | extras_element extras_elements;

extras_element::= extras_weapons
                    | extras_bonus;

extras_weapons::= INIT_WEAPONS weapons_elements END_WEAPONS;

weapons_elements::= weapons_element
                    | weapons_element weapons_elements;

weapons_element::= ID O_PAREN INT COMMA INT C_PAREN SEMICOLON;

extras_bonus::= INIT_BONUS bonus_elements END_BONUS;

bonus_elements::= bonus_element
                | bonus_element bonus_elements;

bonus_element::= ID O_PAREN INT COMMA INT C_PAREN SEMICOLON;

finish::= INIT_FINISH finish_elements END_FINISH;

finish_elements::= finish_element
                    | finish_element finish_elements;

finish_element::= ID O_PAREN INT COMMA INT C_PAREN SEMICOLON;

int_range::= INT
            | INT DOT DOT INT;

